// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.6.10'
    ext.dokka_version = '1.4.10.2'

    repositories {
        google()
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.6.4"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath 'digital.wup:android-maven-publish:3.6.2'
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.11.2")
}

allprojects {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        jcenter() // Warning: this repository is going to shut down soon

        maven {
            credentials {
                username project.hasProperty('SDK_MAPPY_NAME') ? project.property('SDK_MAPPY_NAME') : System.getenv('SDK_MAPPY_NAME')
                password project.hasProperty('SDK_MAPPY_PWD') ? project.property('SDK_MAPPY_PWD') : System.getenv('SDK_MAPPY_PWD')
            }
            url "http://sdkandroid.mappy.net/snapshot/"
        }
        maven {
            credentials {
                username project.hasProperty('SDK_MAPPY_NAME') ? project.property('SDK_MAPPY_NAME') : System.getenv('SDK_MAPPY_NAME')
                password project.hasProperty('SDK_MAPPY_PWD') ? project.property('SDK_MAPPY_PWD') : System.getenv('SDK_MAPPY_PWD')
            }
            url "http://sdkandroid.mappy.net/"
        }
    }

}

/// task use for MULTI module projects
/// see https://detekt.github.io/detekt/baseline.html#gradle
task detektProjectBaseline(type: io.gitlab.arturbosch.detekt.DetektCreateBaselineTask) {
    description = "Overrides current baseline."
    ignoreFailures.set(true)
    parallel.set(true)
    buildUponDefaultConfig.set(true)
    setSource(files(rootDir))
    config.setFrom(files("$rootDir/script/detekt/detekt-config.yml"))
    baseline.set(file("$rootDir/script/detekt/detekt-baseline.xml"))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/resources/**")
    exclude("**/build/**")
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

