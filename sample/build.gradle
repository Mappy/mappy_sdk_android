apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId "com.mappy.sdk.sample"
        minSdkVersion 21
        targetSdkVersion 30

        resConfigs "fr"
        versionCode 1
        versionName "7.1.2"
        testInstrumentationRunner "com.mappy.MultiDexAndroidJUnitRunner"

        multiDexEnabled true
        generatedDensities = []

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "default"

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            shrinkResources false
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.debug
            shrinkResources true
            minifyEnabled true
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/androidx.fragment_fragment.version'
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "3g"
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

dependencies {
    def lifecycle_version = "2.3.1"
    def activity_version = "1.3.0-alpha07"

    implementation "com.mappy:mappy-sdk:7.1.4"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //to parse csv files. For sample only
    implementation 'commons-io:commons-io:2.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.activity:activity-ktx:$activity_version"

    // Other
    implementation 'com.univocity:univocity-parsers:1.0.0'

}

android.testVariants.all { variant ->
    variant.getCompileConfiguration().exclude group: 'com.android.support', module: 'support-v4'
    variant.getRuntimeConfiguration().exclude group: 'com.android.support', module: 'support-v4'
}

android.applicationVariants.all { variant ->
    variant.outputs.all { //output ->
        //def file = output.outputFile
        //file = new File(file.parent, file.name.replace(".apk", "-${android.defaultConfig.versionName}.apk"))
        //output.outputFile = new File(file.parent, file.name.replace("-unsigned", ""))
        outputFileName = outputFileName.replace(".apk", "-${android.defaultConfig.versionName}.apk").replace("-unsigned", "")
    }
}